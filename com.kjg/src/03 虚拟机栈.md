# JVM与内存

JVM有自己的一套内存管理机制，该机制规定了Java进程运行过程中对内存的申请、分配、管理的策略，但是不同的JVM的内存管理机制有一定的差异。**其中运行时数据区可以理解为JVM所管理的主要内存区域**。对于运行时数据区的划分，可以参考下图：

![image](https://user-images.githubusercontent.com/48977889/150280032-3a1ce1cf-a056-494b-8598-8a9ab81f58f4.png)

其中代码缓存+元数据区可以理解为方法区。需要注意的是，一个JVM只会有一个运行时数据区，**JVM本身会维护运行时数据区的一个映射对象：即java.lang.Runtime对象**。

对于运行时数据区来说，可以划分为：线程私有区域、线程共享区域。

线程私有区域包括：PC、本地方法栈、虚拟机栈。

线程共享区域包括：方法区、堆。

也就是说：方法区、堆里的数据需要考虑线程安全问题，**注意！不一定是所有堆的数据都会有安全问题，毕竟虚拟机栈的一些指针可能指向堆里，但只有栈所属的线程能访问**。

# Java线程

在HotSpot虚拟机里，一个Java线程会映射为一个lwp线程，这个lwp线程会随着Java线程的创建而创建、终止而回收，这里就体现了线程池的重要性。当lwp线程初始化成功后就调用Java线程的run方法。

在JVM中除了程序线程外，还有其他的Java线程，这些线程维护着进程的运行，包括：虚拟机线程、周期任务线程、GC线程、编译线程、信号调度线程。这也是为什么用jconsole查看java进程时会发现还有其他多个线程。

# PC寄存器

这里的PC寄存器和OS的PC寄存器不是同一个东西，**可以理解为是模仿OS的，一个JVM层面的PC，在OS层面这个PC是一块内存空间**，作用是记录PC持有者（线程）**正在**执行的字节码指令的**地址**（非本地方法）。

有以下代码：

```java
public class PcCounter {
    public static void main(String[] args) {
        int i = 10;
        int j = 11;
        int k = i + j;

        String s = "abc";
        System.out.println(i);
        System.out.println(k);
    }
}
```

通过javap -v PcCounter.class获取对应的字节码：

```
Classfile /home/kjg/projects/java/jvm_study/out/production/jvm_study/src/code/PcCounter.class
  Last modified 2022-1-24; size 644 bytes
  MD5 checksum a2a481d1ff3156207b4a69ee1c3b71f0
  Compiled from "PcCounter.java"
public class src.code.PcCounter
  minor version: 0
  major version: 52
  flags: ACC_PUBLIC, ACC_SUPER
Constant pool:
   #1 = Methodref          #6.#26         // java/lang/Object."<init>":()V
   #2 = String             #27            // abc
   #3 = Fieldref           #28.#29        // java/lang/System.out:Ljava/io/PrintStream;
   #4 = Methodref          #30.#31        // java/io/PrintStream.println:(I)V
   #5 = Class              #32            // src/code/PcCounter
   #6 = Class              #33            // java/lang/Object
   #7 = Utf8               <init>
   #8 = Utf8               ()V
   #9 = Utf8               Code
  #10 = Utf8               LineNumberTable
  #11 = Utf8               LocalVariableTable
  #12 = Utf8               this
  #13 = Utf8               Lsrc/code/PcCounter;
  #14 = Utf8               main
  #15 = Utf8               ([Ljava/lang/String;)V
  #16 = Utf8               args
  #17 = Utf8               [Ljava/lang/String;
  #18 = Utf8               i
  #19 = Utf8               I
  #20 = Utf8               j
  #21 = Utf8               k
  #22 = Utf8               s
  #23 = Utf8               Ljava/lang/String;
  #24 = Utf8               SourceFile
  #25 = Utf8               PcCounter.java
  #26 = NameAndType        #7:#8          // "<init>":()V
  #27 = Utf8               abc
  #28 = Class              #34            // java/lang/System
  #29 = NameAndType        #35:#36        // out:Ljava/io/PrintStream;
  #30 = Class              #37            // java/io/PrintStream
  #31 = NameAndType        #38:#39        // println:(I)V
  #32 = Utf8               src/code/PcCounter
  #33 = Utf8               java/lang/Object
  #34 = Utf8               java/lang/System
  #35 = Utf8               out
  #36 = Utf8               Ljava/io/PrintStream;
  #37 = Utf8               java/io/PrintStream
  #38 = Utf8               println
  #39 = Utf8               (I)V
{
  public src.code.PcCounter();
    descriptor: ()V
    flags: ACC_PUBLIC
    Code:
      stack=1, locals=1, args_size=1
         0: aload_0
         1: invokespecial #1                  // Method java/lang/Object."<init>":()V
         4: return
      LineNumberTable:
        line 3: 0
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0       5     0  this   Lsrc/code/PcCounter;

  public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=5, args_size=1
         0: bipush        10
         2: istore_1
         3: bipush        11
         5: istore_2
         6: iload_1
         7: iload_2
         8: iadd
         9: istore_3
        10: ldc           #2                  // String abc
        12: astore        4
        14: getstatic     #3                  // Field java/lang/System.out:Ljava/io/PrintStream;
        17: iload_1
        18: invokevirtual #4                  // Method java/io/PrintStream.println:(I)V
        21: getstatic     #3                  // Field java/lang/System.out:Ljava/io/PrintStream;
        24: iload_3
        25: invokevirtual #4                  // Method java/io/PrintStream.println:(I)V
        28: return
      LineNumberTable:
        line 5: 0
        line 6: 3
        line 7: 6
        line 9: 10
        line 10: 14
        line 11: 21
        line 12: 28
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0      29     0  args   [Ljava/lang/String;
            3      26     1     i   I
            6      23     2     j   I
           10      19     3     k   I
           14      15     4     s   Ljava/lang/String;
}
SourceFile: "PcCounter.java"

```

第69-85行是PcCounter的字节码内容，其中字节码前面的数字是指令地址，即**PC寄存器存放的内容**，执行引擎从PC寄存器拿到指令地址、根据地址拿到指令内容、根据指令内容执行（最终是翻译成机器指令，让CPU去运行，之后就是OS的事了）。**执行引擎在执行指令的过程中会操作局部变量表、操作数栈等运行时数据区。**

我们来看下这段字节码的部分内容：

1.“10: ldc           #2”：从常量池中取出一个常量，符号引用为#2。

2.根据#2这个符号在常量池找到“String             #27”，从而找到符号引用#27。

3.根据#27这个符号引用在常量池找到“#27 = Utf8               abc”，即字符串“abc”。

所以指令地址为10的指令 ldc #2其实做了多个操作。

# 虚拟机栈

虚拟机栈没有GC，但会有OOM，它主要记录**方法**运行的数据，而堆主要记录存储的数据。运行数据包含：方法局部变量、部分结果（方法运行时的中间结果）、方法返回值（TODO 到底是本方法返回值，还是派生方法返回值？）。其中方法局部变量包含基本类型变量、引用类型变量。如果是基本类型则直接存储值；引用类型则存储变量的指针，这个指针指向堆的实际数据。

虚拟机栈的基本存储单位是：**栈帧**。当一个方法A被调用时，方法A会被包装成一个栈帧压入栈内，方法A结束后（return和exception）其代表的栈帧会出栈。如果方法A调用了方法B，同理方法B也会被包装成栈帧压入栈内。对于JVM栈来说：栈顶所代表的方法为“**当前方法**”。

JVM规范规定了栈的大小是可以动态扩容或固定不变，当方法嵌套调用太多时，比如MethodA调用MethodB，MethodB又调用MethodA，大量的栈帧压入栈内，当栈内栈帧数超过虚拟机栈的大小后会抛出StackOverFlow异常。如果设置栈为动态大小，当JVM对栈进行动态扩容时发现内存不足（毕竟栈本质是内存空间）后会抛出OutOfMemory异常。对于栈设置可以通过**JVM参数**改动。

![2e9d06e558f7dbfc8d8dadb0bd78ca3](https://user-images.githubusercontent.com/48977889/152633683-0dacd92d-4bb1-43be-869f-b714425b28c9.png)

作为虚拟机栈的基本存储单位，栈帧包含：

1.局部变量表

2.操作数栈

3.动态链接

4.方法返回地址（TODO 是本身的返回地址，还是派生的返回地址？）

5.附加信息

## 局部变量表

用来存储方法的局部变量、方法传参，它的本质是一个**一维数组**。它可以存储基本数据类型、引用类型、返回地址。局部变量表的大小在编译器就确定下来了。

局部变量表的基本存储单位是slot，一个slot最大能存放32位的数据。其中byte、short、char在局部变量表里存储时会转为int，boolean也会转为int：0=false，非0=true。有以下代码：

```java
public class LocalVariables {
    public static void main(String[] args) {
        int i = 123;
        LocalVariables localVariables = new LocalVariables();
        double d = (double) 23.2342;
        int j = 456;
    }
}
```

通过javap -v可以获取到main方法的栈信息

![b51e1adaedc99461453940690a996ab](https://user-images.githubusercontent.com/48977889/152634475-dc2603b9-fdd6-498a-b09d-2ea321b21421.png)

可以看到这段代码中main方法所对应的栈帧M局部变量表的大小=6，首先第一个slot的数据是参数args，它是一个String数组，第二个slot存放局部变量i，因为它是32位的，所以占用一个slot。第三个slot存放局部变量localVariables，它是一个引用类型（指向堆中的对象），所以也占用一个slot。重点是第四个slot存放局部变量d，由于它是double型，需要占用两个slot，**因此d实际上是占用了索引等于3和4两个slot**。这也是为什么最后一个局部变量**j占用的slot索引是5**。

值得注意的是：如果这个方法是对象方法或构造方法，方法的会默认带一个this参数，即对象本身，这个this会存放在局部变量表的第一个slot里，如以下代码：

```java
public class LocalVariables {
    public static void main(String[] args) {
        int i = 123;
        LocalVariables localVariables = new LocalVariables();
        double d = (double) 23.2342;
        int j = 456;
        localVariables.testThis(999);
    }

    public void testThis(int x){

    }
}
```

![345399e574ba49107287f8bf39b2f8c](https://user-images.githubusercontent.com/48977889/152667261-b23958b6-4d80-4790-aec8-fceb2e9e74e7.png)

![7c7929f9aa1378869b3974cc2bcfc92](https://user-images.githubusercontent.com/48977889/152667263-98ecdce7-b018-493e-a722-1b7ccd978400.png)

可以看到默认构造方法和testThis方法都包含一个this的局部变量，它都是存放在第0个slot里。

Slot也会被重复利用，有些变量的作用域不是整个方法周期，可能在方法内部的一小段周期内就用完了，在这之后声明的变量可以被插入到过期变量生前存放的slot内，当然，这一步也是编译器决定的。如以下代码：

```java
public void testSlotCircle(){
    int a = 123;
    {
        int b = a + 1;
        // 如果不用b的话，编译器会优化掉这段代码，当作不存在
        System.out.println(b);
    }
    int c = a + 2;
}
```

![be76da326c64f5bb75cdf0227026e0e](https://user-images.githubusercontent.com/48977889/152667264-3747a3ad-9580-4578-a6ee-526a3d9413b9.png)

可以看到b原先占用index= 2的slot，后来生命周期结束后，作用域外的c变量也插入到index=2的slot内。

## 操作数栈

操作数栈的作用是：根据字节码指令出栈入栈，执行特定操作，操作包含复制、交换、求和等，可以理解操作数栈为**数据的临时存放区**。操作数栈的大小由编译器确定下来，有以下代码：

```java
public class OperandStack {
    public static void main(String[] args) {

    }
    public void add() {
        int i = 6;
        int j = 8;
        int k = i + j;
    }
}
```

获取OperandStack反编译内容，可以看到add方法的栈帧信息：

![e6674faabd7c51a87e6c76ba59a5f23](https://user-images.githubusercontent.com/48977889/152670396-43446d87-6560-4b3c-a770-740dd20826dd.png)

可以看到add栈帧操作数栈的大小=2、局部变量表大小=4。以下的具体的字节码指令分析：

```
0: bipush        6		# 往操作数栈压入6
2: istore_1				# 从操作数栈弹出数据6，存放在局部变量表索引=1的slot内
3: bipush        8		# 往操作数栈压入8
5: istore_2				# 从操作数栈弹出数据8，存放在局部变量表索引=2的slot内
6: iload_1				# 从局部变量表取索引=1的数据，将值存放在操作数栈内（注意！不是数据转移，是复制）
7: iload_2				# 从局部变量表取索引=2的数据，将值存放在操作数栈内（注意！不是数据转移，是复制）
						# 此时操作数栈内刚好有两个栈单位，栈底是6，栈顶是8
8: iadd					# 这一步是将操作数栈内的两个栈单位弹出、相加，将结果14压入操作数栈内
9: istore_3				# 从操作数栈弹出数据14，存放在局部变量表索引=3的slot内
10: return				# 方法返回
```

1.局部变量表的赋值是通过操作数栈来完成的

2.一开始压入的是index=1的slot，因为index=0的slot默认是this

3.计算结果也是优先放入操作数栈内，最后才赋值给局部变量表



同时操作数栈还能保存方法的返回值，如以下代码：

```java
public class OperandStack {
    public static void main(String[] args) {

    }
    public void add() {
        int i = getSum();
        int j = 8;
        int k = i + j;
    }

    public int getSum() {
        return 6;
    }
}
```

它的字节码内容是：

```
  public void add();
    descriptor: ()V
    flags: ACC_PUBLIC
    Code:
      stack=2, locals=4, args_size=1
         0: aload_0 													# 从局部变量表中取this放到操作数栈中
         1: invokevirtual #2                  // Method getSum:()I		# 调用this的getSum()方法，将结果压入操作数栈内
         4: istore_1													# 直接从操作数栈内弹出getSum()的返回值，存放到index=1的slot
         5: bipush        8
         7: istore_2
         8: iload_1
         9: iload_2
        10: iadd
        11: istore_3
        12: return
      LineNumberTable:
        line 8: 0
        line 9: 5
        line 10: 8
        line 11: 12
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0      13     0  this   Lsrc/code/OperandStack;
            5       8     1     i   I
            8       5     2     j   I
           12       1     3     k   I

  public int getSum();
    descriptor: ()I
    flags: ACC_PUBLIC
    Code:
      stack=1, locals=1, args_size=1
         0: bipush        6
         2: ireturn
      LineNumberTable:
        line 14: 0
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0       3     0  this   Lsrc/code/OperandStack;
```

getSum()最后用的是ireturn，此时会将**操作数栈内的值**返回到**上一个虚拟机栈帧的操作数栈**内。这也是为什么add的字节码指令是没有bipush 6直接istore_1，直接从操作数栈弹出数据存放到局部变量表index=1的slot中，这个数据就是getSum()的返回值6。至于返回值入上一个栈帧的操作数栈里，**这个操作应该是执行引擎提前做好了**。

## 动态链接

讲动态链接时，我们先看看什么叫符号引用。有以下代码：

```java
public class DynamicLinking {

    int num = 10;

    public void A(){
        B();
    }

    public void B(){
        System.out.println(num);
    }

    public static void main(String[] args) {

    }

}
```

可以看到它的字节码内容：

```
Constant pool:
   #1 = Methodref          #7.#25         // java/lang/Object."<init>":()V
   #2 = Fieldref           #6.#26         // src/code/DynamicLinking.num:I
   #3 = Methodref          #6.#27         // src/code/DynamicLinking.B:()V				// step 3：#3代表#6、#27，去看看#6
   #4 = Fieldref           #28.#29        // java/lang/System.out:Ljava/io/PrintStream;
   #5 = Methodref          #30.#31        // java/io/PrintStream.println:(I)V
   #6 = Class              #32            // src/code/DynamicLinking					// step 4：#6代表#32
   #7 = Class              #33            // java/lang/Object
   #8 = Utf8               num
   #9 = Utf8               I
  #10 = Utf8               <init>
  #11 = Utf8               ()V
  #12 = Utf8               Code
  #13 = Utf8               LineNumberTable
  #14 = Utf8               LocalVariableTable
  #15 = Utf8               this
  #16 = Utf8               Lsrc/code/DynamicLinking;
  #17 = Utf8               A
  #18 = Utf8               B
  #19 = Utf8               main
  #20 = Utf8               ([Ljava/lang/String;)V
  #21 = Utf8               args
  #22 = Utf8               [Ljava/lang/String;
  #23 = Utf8               SourceFile
  #24 = Utf8               DynamicLinking.java
  #25 = NameAndType        #10:#11        // "<init>":()V
  #26 = NameAndType        #8:#9          // num:I									
  #27 = NameAndType        #18:#11        // B:()V									// step 6：同理，#27代表B方法
  #28 = Class              #34            // java/lang/System
  #29 = NameAndType        #35:#36        // out:Ljava/io/PrintStream;
  #30 = Class              #37            // java/io/PrintStream
  #31 = NameAndType        #38:#39        // println:(I)V
  #32 = Utf8               src/code/DynamicLinking									// step 5：#32代表这个类的名字，再去看看#27
  #33 = Utf8               java/lang/Object
  #34 = Utf8               java/lang/System
  #35 = Utf8               out
  #36 = Utf8               Ljava/io/PrintStream;
  #37 = Utf8               java/io/PrintStream
  #38 = Utf8               println
  #39 = Utf8               (I)V
{
  int num;
    descriptor: I
    flags:

  public src.code.DynamicLinking();
    descriptor: ()V
    flags: ACC_PUBLIC
    Code:
      stack=2, locals=1, args_size=1
         0: aload_0
         1: invokespecial #1                  // Method java/lang/Object."<init>":()V
         4: aload_0
         5: bipush        10
         7: putfield      #2                  // Field num:I
        10: return
      LineNumberTable:
        line 3: 0
        line 5: 4
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0      11     0  this   Lsrc/code/DynamicLinking;

  public void A();
    descriptor: ()V
    flags: ACC_PUBLIC
    Code:
      stack=1, locals=1, args_size=1
         0: aload_0													// step 1：加载this到操作数栈
         1: invokevirtual #3                  // Method B:()V		// step 2:通过invokevirtual调用#3，此时去类常量池看看#3是什么
         4: return
      LineNumberTable:
        line 8: 0
        line 9: 4
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0       5     0  this   Lsrc/code/DynamicLinking;

  public void B();
    descriptor: ()V
    flags: ACC_PUBLIC
    Code:
      stack=2, locals=1, args_size=1
         0: getstatic     #4                  // Field java/lang/System.out:Ljava/io/PrintStream;
         3: aload_0
         4: getfield      #2                  // Field num:I
         7: invokevirtual #5                  // Method java/io/PrintStream.println:(I)V
        10: return
      LineNumberTable:
        line 12: 0
        line 13: 10
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0      11     0  this   Lsrc/code/DynamicLinking;
```

字节码指令中带#符号的就是符号引用，它用来指向某一个资源（变量、类、方法等）。可以在字节码的常量池内通过符号引用找到对应的资源。每个字节码有自身的常量池。java代码运行时，会将字节码的常量池加载到方法区内，成为**运行时常量池**。每一个栈帧内部都包含着一个指向**运行时常量池**的**方法引用区**里，这个的**栈帧所代表的方法的引用**，动态链接的过程是在**运行期间**通过符号引用+这个**引用**在运行时常量池找到这个方法的直接引用。

![110d3b3455068533da4de5ef9bcf78e](https://user-images.githubusercontent.com/48977889/153411909-f47d87a3-8b57-4f78-bf55-5107c666a650.png)

与动态链接相对应的是静态链接，如果字节码在编译期就能确定被调用的目标方法（非虚方法），那么编译后字节码指令引用的方法可以直接从符号引用转为直接引用，**转为直接引用后不会再改变**。简单来说，调用非虚方法的指令用的是静态链接。

比如invokeVirtual和invokeInterface，这两个指令是动态链接的指令。invokespecial是静态链接的指令。

在Java中，构造器、私有方法、静态方法、final方法、父类方法都是非虚方法，其他为虚方法。**虚方法是保证多态的必要条件**。

对于以下指令，调用不同的方法：

1.invokestatic：调用静态方法（非虚）。

2.invokespecial：调用构造方法、私有、super方法（非虚）。

3.invokevirtual：调用普通的对象方法（既有虚方法、也有非虚方法），包含final方法。

4.invokeinterface：调用接口方法。

5.invokedynamic：可以有用户确定方法版本

## 方法重写的本质

有以下代码：

```java
public class Liquor {
    public void drink(){
        System.out.println("drink normal liquor");
    }

    public static void main(String[] args) {
        Liquor liquor = new Beer();
        liquor.drink();
    }

    public static void invoke(Liquor liquor){
        liquor.drink();
    }

}

class Beer extends Liquor{

    public void drink(){
        System.out.println("Nice Beer!!!!");
    }
}
```

对应字节码内容：

![image](https://user-images.githubusercontent.com/48977889/154022854-3227eed1-77f0-4da0-8484-f5cf118eb2d3.png)

调用对象方法的时候首先会调用aload指令获取**实际对象**到操作数栈，然后通过i对应的invoke指令+常量池方法描述，invoke指令会将操作数栈的**实际对象**和方法参数弹出来，方法描述是用来比对这个**实际对象**是否有符合**方法描述**的方法实现，如果比对正确则校验权限，权限校验通过了则获取方法的直接引用。当然，**这个过程是由执行引擎来完成的**。伪代码如下（this指的是实际对象）

if this.type == 常量池方法描述{

​	if 符合权限调用{
​		获取直接引用

​	}else{

​		抛出非法访问异常

​	}

}else{

​	根据继承关系往上排查，查看父类有没有符合常量池方法描述的，如果有，则校验权限；如果没有，则继续往上排查。

​	如果排查到Object仍没有，则抛出抽象方法异常。

}

由此可见，调用方法时优先查看自己有没有自己实现的方法（方法描述一致），如果没有则逐层往上排查。但是逐层的排查非常浪费时间，因此在类加载阶段创建一个虚方法表，**每个类对应一个虚方法表**，当这个类的对象要调用虚方法时，直接在这个类虚方法表里查找具体的实现，然后调用。

## 方法返回地址

要明确一点：方法返回地址与**方法返回值地址**是不一样的。 方法返回地址本质存的是调用者的下一条指令的PC值。如A方法执行B方法后会执行C指令。当A方法调用B方法时，会将C指令的地址放到B方法栈帧的方法返回地址内。等到B调用完后，这个线程的PC值会重置为B的方法返回地址。

如果B方法发生了异常，执行引擎不会将B的方法返回地址返回，而是检查A方法的异常表（通常trycatch会转义成异常表）有没有对应异常的处理过程，如果有则调用对应处理过程，没有则向上抛出（变为A抛出异常）。

## 栈数据常见问题点

1.方法定义的局部变量不一定是线程安全的，如果变量发生了逃逸（作用域在方法之外，如返回、传递），那么变量就变得不安全；反之则安全。

# 本地方法栈

要知道，Java本身运行在JVM上，JVM是一个运行在操作系统的进程，所以Java本身无法直接操作系统函数，如epoll、创建内核线程等...因此JVM里设计了本地方法。

本地方法本质上是c语言、c++（JVM实际上没有规范语言类别）编写的程序，用来完成如创建内核线程、执行系统调用的**操作系统级别函数**。当JVM调用本地方法时，**执行引擎**会找到该本地方法对应的、由外部语言实现的函数来调用。本地方法和普通方法一样，被调用的时候会包装成一个栈帧，压入本地方法栈内。

值得注意的是，当本地方法被调用后，实际执行的操作就变得和JVM同级别了，都属于在操作系统上运行的进程，它甚至可以获取JVM的信息，可以理解此时的Java线程陷入了内核态。

HotSot虚拟机中，将本地方法栈和虚拟机栈合二为一，即普通方法和本地方法都作为栈帧压入虚拟机栈内。

